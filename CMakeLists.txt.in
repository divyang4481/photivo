################################################################################
##
## Photivo
##
## Copyright (C) 2013 Sergey Salnikov <salsergey@gmail.com>
##
## This file is part of Photivo.
##
## Photivo is free software: you can redistribute it and/or modify
## it under the terms of the GNU General Public License version 3
## as published by the Free Software Foundation.
##
## Photivo is distributed in the hope that it will be useful,
## but WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
## GNU General Public License for more details.
##
## You should have received a copy of the GNU General Public License
## along with Photivo.  If not, see <http://www.gnu.org/licenses/>.
##
################################################################################
#
# The file CMakeLists.txt is generated automatically and you shouldn't
# edit it directly. Instead of this you should edit CMakeLists.txt.in
# and then run a script CMakeGenerate.py.
# The script extracts all sources, headers and UIs from the
# photivoProject/photivoProject.pro file and includes them into
# CMakeLists.txt. So these variables should be lived empty in CMakeLists.txt.in.
#
################################################################################

CMAKE_MINIMUM_REQUIRED( VERSION 2.8 )

PROJECT( photivo )

if( NOT CMAKE_CXX_COMPILER_VERSION VERSION_GREATER 4.6 )
  message( SEND_ERROR "\nAt least GCC 4.6 is required to build Photivo." )
endif( NOT CMAKE_CXX_COMPILER_VERSION VERSION_GREATER 4.6 )

if( NOT CMAKE_BUILD_TYPE )
  set( CMAKE_BUILD_TYPE Release )
endif( NOT CMAKE_BUILD_TYPE )

find_package( PkgConfig REQUIRED )
find_package( JPEG REQUIRED )
find_package( Qt4 4.7 REQUIRED QtCore QtGui QtNetwork )
include( ${QT_USE_FILE} )

pkg_check_modules( GLIB2   REQUIRED glib-2.0>=2.18 )
pkg_check_modules( LCMS2   REQUIRED lcms2>=2.1 )
pkg_check_modules( EXIV2   REQUIRED exiv2>=0.19 )
pkg_check_modules( LQR     REQUIRED lqr-1>=0.4.1 )
pkg_check_modules( LENSFUN REQUIRED lensfun>=0.2.5 )
pkg_check_modules( GM      REQUIRED GraphicsMagick++>=1.3.12 )
pkg_check_modules( GMW     REQUIRED GraphicsMagickWand>=1.3.12 )
pkg_check_modules( FFTW3   REQUIRED fftw3>=3.2.2 )

if( ${WITH_GIMP} )
  pkg_check_modules( GIMP REQUIRED gimp-2.0>=2.6.10 )
  pkg_check_modules( GTK2 REQUIRED gtk+-2.0 )
endif( ${WITH_GIMP} )

EXECUTE_PROCESS( COMMAND sh get_appversion
                 WORKING_DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/photivoProject"
                 OUTPUT_STRIP_TRAILING_WHITESPACE OUTPUT_VARIABLE APPVERSION )
message( "\nPhotivo ${APPVERSION}\n" )

option( WITH_CLEAR          "whether to build ptClear" ON )
option( WITH_GIMP           "whether to build Gimp plugin" )
option( WITH_CURVES         "whether to build curves creator" )
option( WITH_ADOBE_PROFILES "whether to build Adobe profiles creator" )

message( "Build Photivo                : ON" )
message( "Build ptClear                : ${WITH_CLEAR}" )
message( "Build Gimp plugin            : ${WITH_GIMP}" )
message( "Build curves creator         : ${WITH_CURVES}" )
message( "Build Adobe profiles creator : ${WITH_ADOBE_PROFILES}\n" )

include_directories( ${CMAKE_CURRENT_SOURCE_DIR}/Sources
                     ${CMAKE_CURRENT_BINARY_DIR}
                     ${QT4_INCLUDES}
                     ${GLIB2_INCLUDE_DIRS}
                     ${JPEG_INCLUDE_DIR}
                     ${EXIV2_INCLUDE_DIRS}
                     ${LCMS2_INCLUDE_DIRS}
                     ${LQR_INCLUDE_DIRS}
                     ${LENSFUN_INCLUDE_DIRS}
                     ${GM_INCLUDE_DIRS}
                     ${GMW_INCLUDE_DIRS}
                     ${FFTW3_INCLUDE_DIRS}
)
link_directories( ${GLIB2_LIBRARY_DIRS}
                  ${JPEG_LIBRARY_DIRS}
                  ${EXIV2_LIBRARY_DIRS}
                  ${LCMS2_LIBRARY_DIRS}
                  ${LQR_LIBRARY_DIRS}
                  ${LENSFUN_LIBRARY_DIRS}
                  ${GM_LIBRARY_DIRS}
                  ${GMW_LIBRARY_DIRS}
                  ${FFTW3_LIBRARY_DIRS}
)

set( photivo_MOC_HDRS

)

set( photivo_SRCS

)

set( photivo_UI_HDRS

)

set( photivo_RCCS
     qrc/photivo.qrc
)

QT4_WRAP_UI( photivo_UI_SRCS ${photivo_UI_HDRS} )
QT4_WRAP_CPP( photivo_MOC_SRCS ${photivo_MOC_HDRS} )
QT4_ADD_RESOURCES( photivo_RCC_SRCS ${photivo_RCCS} )

set( CMAKE_CXX_FLAGS_RELWITHDEBINFO "-O2 -g -Wno-unknown-pragmas" )
set( CMAKE_CXX_FLAGS_RELEASE "-O3 -DNDEBUG -funroll-loops -fopenmp" )
set( CMAKE_C_FLAGS_RELWITHDEBINFO "-O2 -g -Wno-unknown-pragmas" )
set( CMAKE_C_FLAGS_RELEASE "-O3 -DNDEBUG -funroll-loops -fopenmp" )
set( CMAKE_L_FLAGS_RELWITHDEBINFO "-rdynamic" )
set( CMAKE_L_FLAGS_DEBUG "-rdynamic" )

add_executable( photivo ${photivo_SRCS} ${photivo_RCC_SRCS} ${photivo_MOC_SRCS} ${photivo_UI_SRCS} )
set_target_properties( photivo PROPERTIES COMPILE_FLAGS
                       "-ffast-math -std=gnu++0x -DAPPVERSION='${APPVERSION}' -DPREFIX=${CMAKE_INSTALL_PREFIX} -DDLRAW_HAVE_GIMP" )
target_link_libraries( photivo
                       ${GLIB2_LIBRARIES}
                       ${QT_LIBRARIES}
                       ${JPEG_LIBRARIES}
                       ${EXIV2_LIBRARIES}
                       ${LCMS2_LIBRARIES}
                       ${LQR_LIBRARIES}
                       ${LENSFUN_LIBRARIES}
                       ${GM_LIBRARIES}
                       ${GMW_LIBRARIES}
                       ${FFTW3_LIBRARIES}
)

install( TARGETS   photivo         DESTINATION bin )
install( FILES     qrc/photivo-appicon.png DESTINATION share/pixmaps )
install( FILES     ReferenceMaterial/photivo.desktop DESTINATION share/applications )
install( DIRECTORY ChannelMixers   DESTINATION share/photivo )
install( DIRECTORY Curves          DESTINATION share/photivo )
install( DIRECTORY LensfunDatabase DESTINATION share/photivo )
install( DIRECTORY Presets         DESTINATION share/photivo )
install( DIRECTORY Profiles        DESTINATION share/photivo )
install( DIRECTORY Themes          DESTINATION share/photivo )
install( DIRECTORY Translations    DESTINATION share/photivo )
install( DIRECTORY UISettings      DESTINATION share/photivo )


if( ${WITH_CLEAR} )
  add_executable( ptClear Sources/ptClear.cpp )
  target_link_libraries( ptClear ${QT_LIBRARIES} )
  install( TARGETS ptClear DESTINATION bin )
endif( ${WITH_CLEAR} )


if( ${WITH_GIMP} )
  include_directories( ${GIMP_INCLUDE_DIRS} ${GTK2_INCLUDE_DIRS} )
  link_directories(    ${GIMP_LIBRARY_DIRS} ${GTK2_LIBRARY_DIRS} )
  add_executable( ptGimp Sources/ptCalloc.cpp
                         Sources/ptError.cpp
                         Sources/ptGimp.cpp )
  set_target_properties( ptGimp PROPERTIES COMPILE_FLAGS "-ffast-math -std=gnu++0x" )
  target_link_libraries( ptGimp ${QT_LIBRARIES} ${GTK2_LIBRARIES} ${GIMP_LIBRARIES} )
  install( TARGETS   ptGimp                DESTINATION lib/gimp/2.0/plug-ins )
  install( PROGRAMS "mm extern photivo.py" DESTINATION lib/gimp/2.0/plug-ins )
endif( ${WITH_GIMP} )


if( ${WITH_CURVES} )
  add_executable( ptCreateCurves Sources/ptCreateSomeCurves.cpp
                                 Sources/ptCurve.cpp
                                 Sources/ptError.cpp
                                 Sources/ptCalloc.cpp )
  target_link_libraries( ptCreateCurves ${QT_LIBRARIES} )
  install( TARGETS ptCreateCurves DESTINATION bin )
endif( ${WITH_CURVES} )


if( ${WITH_ADOBE_PROFILES} )
  add_executable( ptCreateAdobeProfiles Sources/ptCreateAdobeProfiles.cpp )
  set_target_properties( ptCreateAdobeProfiles PROPERTIES COMPILE_FLAGS "-std=gnu++0x" )
  target_link_libraries( ptCreateAdobeProfiles ${QT_LIBRARIES} ${LCMS2_LIBRARIES} )
  install( TARGETS ptCreateAdobeProfiles DESTINATION bin )
endif( ${WITH_ADOBE_PROFILES} )


# uninstall target
configure_file( "${CMAKE_CURRENT_SOURCE_DIR}/cmake_uninstall.cmake.in"
                "${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake"
                IMMEDIATE @ONLY )

add_custom_target( uninstall COMMAND ${CMAKE_COMMAND} -P ${CMAKE_CURRENT_BINARY_DIR}/cmake_uninstall.cmake )
